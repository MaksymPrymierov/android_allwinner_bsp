# Configuration, Source should be one of the: sdcard, emmc or network

setenv source sdcard
setenv netboot_ip 172.28.184.24
setenv dtb_id 0

#

setenv knladdr 0x40008000
setenv dtbaddr ${fdt_addr_r}
setenv loadaddr ${kernel_addr_r}
setenv dtboaddr 0x52008000

setenv bootargs "
 init=/init rootwait ro androidboot.boottime=223.708 androidboot.selinux=permissive androidboot.hardware=plus2e
 androidboot.revision=2.0 androidboot.board_id=0x1234567 init_time=1552948124 androidboot.serialno=0585
 skip_initramfs androidboot.slot_suffix=_a firmware_class.path=/vendor/etc/firmware
"

setenv bootcmd_bcb '
 bcb load $mmc_index misc ;
 bcb test command = bootonce-bootloader && bcb clear command && bcb store && fastboot udp ;
 bcb clear command ;
 bcb store ;
'

setenv bootcmd_prepare_env '
 setenv mmc_index 0 ;
 setenv bootdevice_path \"platform/soc/1c0f000.mmc\";
 if test \"${source}\" = \"emmc\";
 then
  setenv mmc_index 1;
  setenv bootdevice_path \"platform/soc/<TODO put path here>\";
 fi;
 if test \"${source}\" = \"network\";
 then
  setenv bootdevice_path \"virtual/block/nbd0\";
  setenv bootargs \"\$bootargs nbdroot=\${netboot_ip}:9000/ ip=dhcp root=/dev/nbd0p8 rootdelay=3\";
 fi;
 setenv bootargs \"\$bootargs androidboot.bootdevice=\${bootdevice_path}\" ;
'

setenv bootcmd_start '
 dtimg start \$knladdr \$dtb_id dtb_start &&
 dtimg size \$knladdr \$dtb_id dtb_size &&
 cp.b \$dtb_start \$dtbaddr \$dtb_size &&
 fdt addr \$dtbaddr &&
 dtimg start \$knladdr 1 dtb_start &&
 dtimg size \$knladdr 1 dtb_size &&
 cp.b \$dtb_start \$dtboaddr \$dtb_size &&
 fdt resize 8192 &&
 fdt apply \$dtboaddr &&
 fdt set /firmware/android/fstab/vendor/ dev "/dev/block/\${bootdevice_path}/by-name/vendor" &&
 bootm \$loadaddr
'

setenv bootcmd_block '
 run bootcmd_bcb &&
 part uuid mmc \$mmc_index:6 system_a_uuid &&
 setenv bootargs \"\$bootargs root=PARTUUID=\${system_a_uuid}\" &&
 part start mmc \$mmc_index dtb_a dtb_start &&
 part size mmc \$mmc_index dtb_a dtb_size &&
 part start mmc \$mmc_index boot_a boot_start &&
 part size mmc \$mmc_index boot_a boot_size &&
 mmc dev \$mmc_index &&
 mmc read \$knladdr \$dtb_start \$dtb_size &&
 mmc read \$loadaddr \$boot_start \$boot_size
'

setenv bootcmd_network '
 setenv ethaddr 98:4F:EE:0C:C8:0C &&
 dhcp \$knladdr \$netboot_ip:dtb.img &&
 dhcp \$loadaddr \$netboot_ip:boot.img
'

setenv bootcmd '
 run bootcmd_prepare_env ;
 if test \"${source}\" = \"network\" ;
 then
  run bootcmd_network ;
 else
  run bootcmd_block ;
 fi;
 run bootcmd_start ;
'

run bootcmd
